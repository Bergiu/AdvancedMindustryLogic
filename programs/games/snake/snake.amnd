import ../../../stdlib/display/get_pos_center.amnd

# snake:
# - playground:
#   - length and width
#   - snake
#   - fruits

# 1. create a fruit on a random position, after startup and after one fruit is eaten
# 2. a field in the playground can be filled by nothing, fruit or snake
# 3. snake moves in a direction

# devices
snake_mem = bank1
snake_mem_size = 512
pixel_mem = bank2
pixel_mem_size = 512

if ! snake_mem {
    end
}
if ! pixel_mem {
    end
}

# graphics
display_size_x = 176
display_size_y = 176
amount_fields_x = 20
amount_fields_y = amount_fields_x * (display_size_y / display_size_x )
size_per_field = display_size_x / amount_fields_x
snake_width = ceil(size_per_field)
half_snake_width = snake_width / 2

control enabled switch1 false 0 0 0
sensor reset switch1 @enabled
while not reset {
    sensor reset switch1 @enabled
}
label_reset_game:
control enabled switch1 false 0 0 0

# game
points = 0
sleep_default = 30
sleep = sleep_default

# startup
snake_head_x = 0
snake_head_y = 1
dir_north = 0
dir_east = 1
dir_south = 2
dir_west = 3
dir = dir_east

# the snake memory is a cyclic list
# and contains one field for each snake part.
# the last element in the list is the part that
# is removed after each step

# snake
snake_mem_start = 1
snake_mem_length = 2

# clear memory
write 0 snake_mem 0 # removed field
snake_pos = snake_head_x * amount_fields_y + snake_head_y
write snake_pos snake_mem 1 # snake head

# the pixel mem is a list that maps a value to each pixel
pixel_free = 0
pixel_body = 1

# clear memory
print "Cleanup Game."
printflush message1
i = 0
while i < pixel_mem_size {
    write 0 pixel_mem i
    i++
}
print "Game starts."
printflush message1

# clear display
draw clear 255 255 255 0 0 0  # white

# skin support
sensor skin_support switch2 @enabled

function field_coord(x, y, *field_coord::x_from, *field_coord::y_from) {
    field_coord::x_from = floor(x * size_per_field)
    field_coord::y_from = floor(y * size_per_field)
}

function fruit_coord(x, y, *field_coord::x_from, *field_coord::y_from) {
    field_coord::x_from = floor(x * size_per_field) + half_snake_width
    field_coord::y_from = floor(y * size_per_field) + half_snake_width
}

function random_fruit(*random_fruit::x_out, *random_fruit::y_out) {
    # repeat if position is on the snake
    random_fruit::max_x = amount_fields_x
    random_fruit::x_out = floor(rand(random_fruit::max_x))
    random_fruit::max_y = amount_fields_y
    random_fruit::y_out = floor(rand(random_fruit::max_y))
}

function end_game(lost) {
    if lost {
        # draw middle
        draw color 255 0 0 255 0 0
        draw line 0 0 display_size_x display_size_y 0 0
        draw line display_size_x 0 0 display_size_y 0 0
        drawflush display1
        print "Lost the game.\n"
        print "Your score is: "
        print points
        printflush message1
    } else {
        print "You won!\n"
        print "Your score is: "
        print points
        printflush message1
    }
    end
}

function next_pos() {
    exec get_mouse_pos_center(arc1, display1, dposx, dposy, error)
    if abs(dposx) > abs(dposy) {
        if dposx > 0 {
            if dir != dir_west {
                dir = dir_east
            }
        } else {
            if dir != dir_east {
                dir = dir_west
            }
        }
    } else {
        if dposy > 0 {
            if dir != dir_south {
                dir = dir_north
            }
        } else {
            if dir != dir_north {
                dir = dir_south
            }
        }
    }
    if dir == dir_north {
        snake_head_y += 1
    }
    if dir == dir_south {
        snake_head_y -= 1
    }
    if dir == dir_east {
        snake_head_x += 1
    }
    if dir == dir_west {
        snake_head_x -= 1
    }
    if snake_head_x < 0 {
        snake_head_x = amount_fields_x - 1
    }
    if snake_head_y < 0 {
        snake_head_y = amount_fields_y - 1
    }
    if snake_head_x >= amount_fields_x {
        snake_head_x = 0
    }
    if snake_head_y >= amount_fields_y {
        snake_head_y = 0
    }
}

function collect_fruit() {
    points += 1
    exec random_fruit(fruit_x, fruit_y)
}

function next_snake(collected) {
    # snake
    snake_mem_end = (snake_mem_start + snake_mem_length) % snake_mem_size
    snake_pos = snake_head_x * amount_fields_y + snake_head_y
    # expand by one
    write snake_pos snake_mem snake_mem_end
    if not collected {
        # reduce one field
        snake_mem_start = (snake_mem_start + 1) % snake_mem_size
    } else {
        if snake_mem_size <= snake_mem_length {
            exec end_game(false)
        }
        snake_mem_length += 1
    }
    # pixel
    read snake_tail snake_mem snake_mem_start
    write pixel_body pixel_mem snake_pos
    write pixel_free pixel_mem snake_tail
}

function draw_snake() {
    # dynamic draw
    snake_mem_pos = snake_mem_start
    read snake_part snake_mem snake_mem_pos
    snake_part_x = snake_part // amount_fields_y
    snake_part_y = snake_part % amount_fields_y
    exec field_coord(snake_part_x, snake_part_y, x_from, y_from)
    # draw white
    draw color 255 255 255 255 0 0
    draw rect x_from y_from snake_width snake_width 0 0
    snake_mem_pos = (snake_mem_start + snake_mem_length - 1) % snake_mem_size
    read snake_part snake_mem snake_mem_pos
    snake_part_x = snake_part // amount_fields_y
    snake_part_y = snake_part % amount_fields_y
    # draw white
    if not skin_support {
        exec field_coord(snake_part_x, snake_part_y, x_from, y_from)
        draw color 0 0 0 255 0 0
        draw rect x_from y_from snake_width snake_width 0 0
    } else {
        exec field_coord(snake_part_x, snake_part_y, x_from, y_from)
        draw color 255 255 255 255 0 0
        draw rect x_from y_from snake_width snake_width 0 0
        exec fruit_coord(snake_part_x, snake_part_y, x_from, y_from)
        draw color 255 255 255 255 0 0
        # remove 1 because image is too big
        tmp = snake_width - 1
        if dir == dir_north {
            rot = 0
        }
        if dir == dir_east {
            rot = -90
        }
        if dir == dir_south {
            rot = 180
        }
        if dir == dir_west {
            rot = 90
        }
        draw image x_from y_from @dagger tmp rot 0
    }
}


function collision_detection(*collected) {
    # snake collision detection
    snake_pos = snake_head_x * amount_fields_y + snake_head_y
    read pixel_status pixel_mem snake_pos
    read snake_tail snake_mem snake_mem_start
    snake_tail_x = snake_tail // amount_fields_y
    snake_tail_y = snake_tail % amount_fields_y
    if (not (snake_head_x == snake_tail_x and snake_head_y == snake_tail_y)) and pixel_status != pixel_free {
        exec end_game(true)
    }
    # fruit detection
    collected = false
    if snake_head_x == fruit_x and snake_head_y == fruit_y {
        exec collect_fruit()
        collected = true
    }
}


function next() {
    exec next_pos()
    exec collision_detection(collected)
    exec next_snake(collected)
    print "Points: "
    print points
}

function draw_all() {
    exec draw_snake()

    # draw middle
    draw color 255 255 0 255 0 0
    draw line 0 0 display_size_x display_size_y 0 0
    draw line display_size_x 0 0 display_size_y 0 0
    # draw linePoly 88 88 10 3 10 0

    # draw fruit
    exec fruit_coord(fruit_x, fruit_y, fruit_x_coord, fruit_y_coord)
    draw color 255 255 255 255 0 0
    # draw rect fruit_x_coord fruit_y_coord snake_width snake_width 0 0
    draw image fruit_x_coord fruit_y_coord @lead snake_width 0 0

    drawflush display1
    printflush message1
}

function check_buttons() {
    sensor reset switch1 @enabled
    if reset {
        jump label_reset_game always 0 0
    }
    sensor skin_support switch2 @enabled
    sensor pause switch3 @enabled
    while pause {
        sensor pause switch3 @enabled
    }
}


exec random_fruit(fruit_x, fruit_y)
exec draw_all()

while true {
    exec next()
    exec draw_all()
    i = 0
    while i < sleep {
        exec check_buttons()
        i++
    }
}

